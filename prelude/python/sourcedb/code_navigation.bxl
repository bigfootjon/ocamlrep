# Copyright (c) Meta Platforms, Inc. and affiliates.
#
# This source code is licensed under both the MIT license found in the
# LICENSE-MIT file in the root directory of this source tree and the Apache
# License, Version 2.0 found in the LICENSE-APACHE file in the root directory
# of this source tree.

load(":merge.bxl", "do_merge")
load(":query.bxl", "get_python_library_manifests_tset_from_target_patterns")

def _build_entry_point(ctx: "bxl_ctx") -> None:
    query = ctx.cquery()
    actions = ctx.actions_factory
    root = ctx.root()

    sources = ["{}/{}".format(root, source) for source in ctx.cli_args.source]
    manifests = get_python_library_manifests_tset_from_target_patterns(
        ctx,
        query,
        actions,
        query.owner(sources),
    )

    # Ensure all source files are materialized
    ctx.output.ensure_multiple(manifests.project_as_args("source_type_artifacts"))

    # Ensure merged sourcedb is materialized
    merged_sourcedb = do_merge(
        ctx,
        actions,
        manifests,
        merger_target = "prelude//python/tools/sourcedb_merger:merge",
        command_category = "pyre_merge_sourcedb",
    )
    ctx.output.print_json({"db": merged_sourcedb.abs_path()})

build = bxl(
    doc = """Build Python sourcedb for Pyre code navigation server.

    It takes a list of file paths, and will find the owner targets for all
    those files and build source-db for those owning targets.
    """,
    impl = _build_entry_point,
    cli_args = {
        "source": cli_args.list(
            cli_args.string(
                doc = "File to build a source db for (relative to source root)",
            ),
        ),
    },
)
